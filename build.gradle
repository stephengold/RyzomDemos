// Gradle build script for the RyzomDemos project

plugins {
    id 'application'
}

ext.jme3Version = '3.6.1-stable' // current version of JMonkeyEngine

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    //options.deprecation = true // to provide detailed deprecation warnings
    options.encoding = 'UTF-8'
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_1_10)) {
        options.release = 8
    }
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
Boolean isMacOS = DefaultNativePlatform.currentOperatingSystem.isMacOsX()

tasks.withType(JavaExec) { // Java runtime options:
    if (isMacOS) {
        jvmArgs '-XstartOnFirstThread'
    } else {
        args '--showSettingsDialog'
    }
    //args '--verbose' // to enable additional log output
    classpath sourceSets.main.runtimeClasspath
    enableAssertions true
    //jvmArgs '-verbose:gc'
    //jvmArgs '-Xms512m', '-Xmx512m' // to enlarge the Java heap
    jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

application {
    mainClass = 'ryzomdemos.BuildCharacter'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of SNAPSHOTs
}

dependencies {
    implementation 'com.github.stephengold:Heart:8.6.0'
    implementation 'com.github.stephengold:Acorus:1.0.0'
    implementation 'com.github.stephengold:Wes:0.7.5'
    implementation 'org.jmonkeyengine:jme3-core:' + jme3Version
    runtimeOnly    'org.jmonkeyengine:jme3-awt-dialogs:' + jme3Version
    implementation 'org.jmonkeyengine:jme3-desktop:' + jme3Version
    runtimeOnly    'org.jmonkeyengine:jme3-lwjgl3:' + jme3Version
    runtimeOnly    'org.jmonkeyengine:jme3-testdata:3.1.0-stable'

    // RyzomDemos doesn't use jme3-jogg nor jme3-plugins
    //  -- they are included solely to avoid warnings from AssetConfig.
    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-plugins:' + jme3Version
}

tasks.register('runDodgerGame', JavaExec) {
    mainClass = 'ryzomdemos.DodgerGame'
}

tasks.register('runStatistics', JavaExec) {
    mainClass = 'ryzomdemos.Statistics'
}

// cleanup tasks:

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanSandbox', 'cleanSOs', 'cleanSummary')

tasks.register('cleanDLLs', Delete) { // extracted Windows native libraries
    delete fileTree(dir: '.', include: '*.dll')
}
tasks.register('cleanDyLibs', Delete) { // extracted macOS native libraries
    delete fileTree(dir: '.', include: '*.dylib')
}
tasks.register('cleanSandbox', Delete) { // Acorus sandbox
    delete 'Written Assets'
}
tasks.register('cleanSOs', Delete) { // extracted Linux and Android native libraries
    delete fileTree(dir: '.', include: '*.so')
}
tasks.register('cleanSummary', Delete) {
    delete 'ryzom-summary.bin'
}
