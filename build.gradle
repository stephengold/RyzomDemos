// Gradle script to build the RyzomDemos project

plugins {
    id 'application' // to build JVM applications
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile).configureEach { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_20)) {
        // Suppress warnings that source value 8 is obsolete.
        options.compilerArgs << '-Xlint:-options'
    }
    options.compilerArgs << '-Xlint:unchecked'
    //options.deprecation = true // to provide detailed deprecation warnings
    options.encoding = 'UTF-8'
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_1_10)) {
        options.release = 8
    }
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
Boolean isMacOS = DefaultNativePlatform.currentOperatingSystem.isMacOsX()

tasks.withType(JavaExec).configureEach { // Java runtime options:
    if (isMacOS) {
        jvmArgs '-XstartOnFirstThread'
    } else {
        args '--showSettingsDialog'
    }
    //args '--verbose' // to enable additional log output
    classpath sourceSets.main.runtimeClasspath
    enableAssertions = true
    //jvmArgs '-verbose:gc'
    jvmArgs '-Xms4g', '-Xmx4g' // to enlarge the Java heap
    jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

application {
    mainClass = 'ryzomdemos.BuildCharacter'
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of snapshots
}

dependencies {
    implementation(libs.heart)
    implementation(libs.acorus)
    implementation(libs.wes)
    implementation(libs.jme3.core)
    runtimeOnly(libs.jme3.awt.dialogs)
    implementation(libs.jme3.desktop)
    runtimeOnly(libs.jme3.lwjgl3)
    runtimeOnly(libs.jme3.testdata.old)

    // RyzomDemos doesn't use jme3-jogg nor jme3-plugins
    //  -- they are included solely to avoid warnings from AssetConfig.
    runtimeOnly(libs.jme3.jogg)
    runtimeOnly(libs.jme3.plugins)
}

tasks.register('runDodgerGame', JavaExec) {
    mainClass = 'ryzomdemos.DodgerGame'
}

tasks.register('runStatistics', JavaExec) {
    mainClass = 'ryzomdemos.Statistics'
}

// Register cleanup tasks:

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanSandbox', 'cleanSOs', 'cleanSummary')

tasks.register('cleanDLLs', Delete) { // extracted Windows native libraries
    delete fileTree(dir: '.', include: '*.dll')
}
tasks.register('cleanDyLibs', Delete) { // extracted macOS native libraries
    delete fileTree(dir: '.', include: '*.dylib')
}
tasks.register('cleanSandbox', Delete) { // Acorus sandbox
    delete 'Written Assets'
}
tasks.register('cleanSOs', Delete) { // extracted Linux and Android native libraries
    delete fileTree(dir: '.', include: '*.so')
}
tasks.register('cleanSummary', Delete) {
    delete 'ryzom-summary.bin'
}
