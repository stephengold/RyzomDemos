// Gradle build script for the RyzomDemos project

plugins {
    id 'application'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext { // current versions of libraries:
    jme3Version = '3.5.0-stable'
    lwjgl3Version = '3.3.0'
}

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    //options.deprecation = true // to provide detailed deprecation warnings
    options.encoding = 'UTF-8'
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform;
Boolean isMacOS = DefaultNativePlatform.currentOperatingSystem.isMacOsX();

tasks.withType(JavaExec) { // Java runtime options:
    if (isMacOS) {
        jvmArgs '-XstartOnFirstThread'
    } else {
        args '--showSettingsDialog'
    }
    //args '--verbose' // to enable additional log output
    classpath sourceSets.main.runtimeClasspath
    enableAssertions true
    //jvmArgs '-verbose:gc'
    //jvmArgs '-Xms512m', '-Xmx512m'
    jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

mainClassName = 'ryzomdemos.BuildCharacter'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

task runDodgerGame(type: JavaExec) {
    mainClass = 'ryzomdemos.DodgerGame'
}

task runStatistics(type: JavaExec) {
    mainClass = 'ryzomdemos.Statistics'
}

repositories {
    //mavenLocal() // to find local SNAPSHOTs of libraries
    mavenCentral()
}

dependencies {
    implementation 'com.github.stephengold:Heart:7.4.1'
    implementation 'com.github.stephengold:Acorus:0.9.10'
    implementation 'com.github.stephengold:Wes:0.7.1'
    implementation 'org.jmonkeyengine:jme3-core:' + jme3Version
    implementation 'org.jmonkeyengine:jme3-desktop:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-lwjgl3:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-testdata:3.1.0-stable'

    // RyzomDemos doesn't use jme3-jogg nor jme3-plugins
    //  -- they are included solely to avoid warnings from AssetConfig.
    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-plugins:' + jme3Version

    // LWJGL 3.3 dependencies that were omitted from JME v3.5:
    runtimeOnly 'org.lwjgl:lwjgl:' + lwjgl3Version + ':natives-macos-arm64'
    runtimeOnly 'org.lwjgl:lwjgl-glfw:' + lwjgl3Version + ':natives-macos-arm64'
    runtimeOnly 'org.lwjgl:lwjgl-jemalloc:' + lwjgl3Version + ':natives-macos-arm64'
    runtimeOnly 'org.lwjgl:lwjgl-openal:' + lwjgl3Version + ':natives-macos-arm64'
    runtimeOnly 'org.lwjgl:lwjgl-opengl:' + lwjgl3Version + ':natives-macos-arm64'
}

// cleanup tasks:

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanSOs', 'cleanSummary', 'cleanWrittenAssets')

task cleanDLLs(type: Delete) { // extracted Windows native libraries
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanDyLibs(type: Delete) { // extracted macOSX native libraries
    delete fileTree(dir: '.', include: '*.dylib')
}
task cleanSOs(type: Delete) { // extracted Linux and Android native libraries
    delete fileTree(dir: '.', include: '*.so')
}
task cleanSummary(type: Delete) {
    delete 'ryzom-summary.bin'
}
task cleanWrittenAssets(type: Delete) { // Acorus output directory
    delete 'Written Assets'
}
